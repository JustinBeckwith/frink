<?xml version="1.0" encoding="utf-8"?>
<s:SkinnableContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark"  xmlns:views="views.*" 
		xmlns:panels="views.panels.*" 
		creationComplete="creationCompleteHandler(event)"
		resize="resizeHandler(event)"
		width="100%" height="100%" backgroundColor="0x000000">
	
	
	<fx:Declarations></fx:Declarations>
	<fx:Script>
		<![CDATA[
			import events.FrinkEvent;
			import events.PanelEvent;
			
			import flash.filters.*;
			
			import mx.events.FlexEvent;
			import mx.events.ResizeEvent;
			
			import views.panels.frinkPanel;
			import views.panels.postList;
			
			
			

			
			//--------------------------------------------------------------------------
			//
			//  Event Handlers
			//
			//--------------------------------------------------------------------------
			
			/**
			 * perform the initial load of the front page, or auto log in
			 **/
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				// if the user has cached credentials, log them in automatically
				FrinkData.instance.addEventListener(FrinkEvent.AUTH_CHANGE, frink_authChangeHandler);
				
				if (FrinkData.instance.hasCredentials) {
					FrinkData.instance.autoLogin();
				} else {
					this.frinkPanels.pnlPostList.loadReddit("");
				}
			}
			
			/**
			 * switch the visible panel based on the clicked button in the toolbar
			 **/
			protected function toolbar_changeHandler(event:Event):void
			{
				// hide all of the panels
				for (var i : int = 1; i<6; i++) {
					var fp : frinkPanel = frinkPanels.getElementAt(i) as frinkPanel;
					fp.visible = false;
					fp.includeInLayout = false;
				}
				
				// move the middle panel out if neccesary
				frinkPanels.x = (this.currentState == "portrait") ? 50 : 201;
				postView.visible = false;
				postView.includeInLayout = false;
				frinkPanels.currentState = "normal";
				
				// show the appropriate panel
				var panel : frinkPanel = null;
				switch(toolbar.selectedPanel) {
					case frinkPanel.FRONT_PAGE:
						panel = frinkPanels.pnlPostList;
						frinkPanels.pnlPostList.loadReddit("");
						break;
					case frinkPanel.ALL_REDDITS:
						panel = frinkPanels.pnlPostList;
						frinkPanels.pnlPostList.loadReddit("all");
						break;
					case frinkPanel.SUB_REDDITS:
						panel = frinkPanels.pnlSubReddits;
						frinkPanels.pnlSubReddits.loadSubReddits();
						break;
					case frinkPanel.PROFILE:
						panel = frinkPanels.pnlProfile;
						break;
					case frinkPanel.MAIL:
						panel = frinkPanels.pnlMail;
						if (FrinkData.instance.isLoggedIn)
							frinkPanels.pnlMail.loadMessages();
						break;
					case frinkPanel.SEARCH:
						panel = frinkPanels.pnlSearch;
						break;
				}

				panel.visible = true;
				panel.includeInLayout = true;
			}
			
			/**
			 * show the selected post
			 **/
			protected function postList_changeHandler(event: PanelEvent) : void {
				var pl : postList = event.list;
				postView.post = pl.selectedPost;
				
				// move the middle panel over
				frinkPanels.x = 50;
				frinkPanels.currentState = "background";
				
				// move out the content view
				postView.visible = true;
				postView.includeInLayout = true;
				
				pl.list.selectedIndex = -1;
			}
			
			

			/**
			 * switch the UI back to normal
			 **/
			protected function postView_closeHandler(event:Event):void
			{
				postView.visible = false;
				postView.includeInLayout = false;
				frinkPanels.x = (this.currentState == "portrait") ? 50 : 201;
				frinkPanels.currentState = "normal";
			}
			
			
			/**
			 *	if a tab that requires an account is visible, hide it on logout 
			 **/
			protected function frink_authChangeHandler(event:FrinkEvent) : void {
				
				if (!FrinkData.instance.isLoggedIn && toolbar.selectedPanel == frinkPanel.MAIL) 
					toolbar.selectButton(toolbar.btnFrontPage);
			}

			
			/**
			 * update orientation fields
			 **/
			protected function resizeHandler(event:ResizeEvent):void
			{
				this.currentState = (screen.width > screen.height) ? "landscape" : "portrait";
			}
			
		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="portrait"/>
		<s:State name="landscape"/>
	</s:states>
	
	<s:SkinnableContainer left="2" top="2" right="2" bottom="2" backgroundColor="0x000000">
		
		<s:Rect id="backgroundRect" left="0" right="0" top="0" bottom="0" radiusX="5" radiusY="5" >
			<s:fill>
				<s:BitmapFill source="@Embed('assets/hash-background.png')" fillMode="repeat" />
			</s:fill>
		</s:Rect>
		
		<views:toolbar change="toolbar_changeHandler(event)" id="toolbar" />
		<views:MiddlePanel id="frinkPanels" postSelected="postList_changeHandler(event)" 
						   width.landscape="550" width.portrait="{this.width - 60}"
						   x.landscape="201" x.portrait="50"
			/>	
		<panels:postView width="100%" height="100%" x.landscape="175" x.portrait="51" visible="false" includeInLayout="false" id="postView" close="postView_closeHandler(event)" />
		
	</s:SkinnableContainer>
	
	
	
</s:SkinnableContainer>
