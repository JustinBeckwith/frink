<?xml version="1.0" encoding="utf-8"?>
<panels:frinkPanel xmlns:fx="http://ns.adobe.com/mxml/2009" 
				   xmlns:s="library://ns.adobe.com/flex/spark" xmlns:panels="views.panels.*" xmlns:controls="controls.*">
	
	<fx:Metadata>
		[Event(name="change", type="flash.events.Event")]
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import events.RedditEvent;
			
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.events.PropertyChangeEvent;
			
			import spark.components.VScrollBar;
			
			
			//--------------------------------------------------------------------------
			//
			//  Variables
			//
			//--------------------------------------------------------------------------
			
			/**
			 *	rolling list of available subreddits
			 **/
			[Bindable]
			protected var reddits : ArrayCollection = new ArrayCollection();
			
			[Bindable]
			protected var page_before : String;
			
			[Bindable]
			protected var page_after : String;
			
			[Bindable]
			public var subReddit : Object;
			
			//--------------------------------------------------------------------------
			//
			//  Methods
			//
			//--------------------------------------------------------------------------
			
			/**
			 * when the ui is shown bind the list of reddits
			 **/
			public function loadSubReddits() : void {
				this.currentState = "loading";
				FrinkData.instance.api.loadSubReddits();
			}
			
			//--------------------------------------------------------------------------
			//
			//  Event Handlers
			//
			//--------------------------------------------------------------------------
			
			/**
			 * show the post details
			 **/
			protected function list_changeHandler(event : Event) : void {
				this.subReddit = list.selectedItem;
				this.dispatchEvent(new Event(Event.CHANGE));
			}
			
			/**
			 * bind the posts to the list
			 **/
			protected function api_redditsLoaded(event : RedditEvent) : void {
				var records : Array = event.result.data.children as Array;
				reddits.addAll(new ArrayCollection(records));
				
				// store the identifiers for the prev and next pages
				this.page_after = event.result.data.after;
				this.page_before = event.result.data.before;
				
				this.currentState = "normal";
			}
			
			
			
			/**
			 * add a scroll handler to the list after created manually
			 **/
			protected function list_creationCompleteHandler(event:FlexEvent):void
			{
				//list.scroller.verticalScrollBar.addEventListener(Event.CHANGE, list_verticalScrollBar_change);
				// Does not work with mouse wheel or arrow buttons due to bug http://bugs.adobe.com/jira/browse/SDK-26533
				/* list.scroller.verticalScrollBar.addEventListener( Event.CHANGE, verticalScrollBarChangeHandler ); */
				list.scroller.viewport.addEventListener( PropertyChangeEvent.PROPERTY_CHANGE, propertyChangeHandler );
				FrinkData.instance.api.addEventListener(RedditEvent.SUBREDDITS_LOADED, api_redditsLoaded);
			}
			
			/**
			 * list scroll event
			 **/
			protected function propertyChangeHandler(event : PropertyChangeEvent) : void {
				if ( event.property == "verticalScrollPosition" ) {
					var vsb:VScrollBar = list.scroller.verticalScrollBar;
					if (vsb.value == vsb.maximum && this.currentState != "loadingMore") {
						// load the next page of posts if at the bottom of the list
						this.currentState = "loadingMore";
						FrinkData.instance.api.loadSubReddits(null, this.page_after);
					}
				}
			}
		]]>
	</fx:Script>
	
	<panels:states>
		<s:State name="normal" />
		<s:State name="loading" />
		<s:State name="loadingMore" />
	</panels:states>
	
	
	<controls:LoadingBox includeIn="loading" />
	<s:VGroup width="100%" height="100%" includeIn="normal, loadingMore">
		<s:List id="list" width="100%" height="100%" dataProvider="{reddits}" itemRenderer="views.renderers.SubRedditDetails"
			skinClass="skins.FrinkListSkin"
			change="list_changeHandler(event)"
			creationComplete="list_creationCompleteHandler(event)" />
		<controls:ListLoadingBox includeIn="loadingMore" />
	</s:VGroup>
	
</panels:frinkPanel>
