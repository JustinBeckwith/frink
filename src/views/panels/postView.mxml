<?xml version="1.0" encoding="utf-8"?>
<s:SkinnableContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" width="100%" height="100%" xmlns:renderers="views.renderers.*" creationComplete="creationCompleteHandler(event)" xmlns:controls="controls.*"
		  backgroundColor="0xCCCCCC" xmlns:panels="views.panels.*" show="showHandler(event)" hide="hideHandler(event)" xmlns:views="views.*" >
	<fx:Metadata>
		[Event(name="close", type="flash.events.Event")]
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import events.FrinkEvent;
			
			import flash.filters.*;
			import flash.net.navigateToURL;
			
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import org.hamcrest.object.nullValue;
			
			import views.modals.SettingsModal;
			
			//--------------------------------------------------------------------------
			//
			//  Variables
			//
			//--------------------------------------------------------------------------
			
			private var _post : Object;
			
			/**
			 * used to maintain state during a login event while up/down voting
			 **/
			private var _up : Boolean;
			
			
			[Bindable]
			public function get post() : Object {
				return _post;
			}
			public function set post(value:Object) : void {
				_post = value;
				
				if (_post != null) {
					if (_post.data.is_self) {
						this.currentState = "loading";
						this.comments.loadComments(_post);
						this.comments.visible = true;
					} else {
						this.comments.visible = false;
					}
					btnComments.currentState = "normal";
					this.browser.visible = !_post.data.is_self;
					
					// set the up/down vote icon glows
					btnUp.currentState = btnDown.currentState = "normal";
					if (_post.data.likes == true) {
						btnUp.currentState = "selected";
					}
					else if (_post.data.likes == false) {
						btnDown.currentState = "selected";
					}
					
					if (post.data && post.data.thumbnail && post.data.thumbnail !='') {
						
						// sometimes thumbnail returns a relative system path from reddit.com/static/something.png
						if ((post.data.thumbnail as String).indexOf("/static/") == 0)
							post.data.thumbnail = "http://www.reddit.com" + post.data.thumbnail;
						
						this.img.source = post.data.thumbnail;
						this.thumbnail.visible = true;
						this.thumbnail.includeInLayout = true;
						
					} else {
						this.thumbnail.visible = false;
						this.thumbnail.includeInLayout = false;
					}
				}
					
			}
			
			//--------------------------------------------------------------------------
			//
			//  Methods
			//
			//--------------------------------------------------------------------------
			
			/**
			 * manage the UI and api calls for a vote
			 * 
			 * the reddit API only takes a +1, 0, -1 for votes.  An upvote after a downvote
			 * simply sends the +1, up after up sends a 0, etc
			 **/
			protected function makeVote(up:Boolean) : void {
				
				if (FrinkData.instance.isLoggedIn) {
					
					// vote logic if the user is logged in
					
					var dir : int = 0;
					
					// figure out if the user likes it, hates it, or is ambivalent
					if (post.data.likes == null) {
						// there hasn't been a vote yet, so just make the call and do the UI work
						dir = up ? 1 : -1;
					} else if (post.data.likes == false) {
						// the post was previously down voted - undo downs but flipsy up votes
						dir = up ? 1 : 0;
					} else if (post.data.likes == true) {
						// the post was previously up voted - undo ups but flipsy downs
						dir = up ? 0 : -1;
					}
					
					// update the underlying data structure and UI elements 
					if (dir == 0) {
						post.data.likes = null;
						btnUp.currentState = "normal";
						btnDown.currentState = "normal";
					} else if (dir == -1) {
						post.data.likes = false;
						btnUp.currentState = "normal";
						btnDown.currentState = "selected";
					} else if (dir == 1) { 
						post.data.likes = true;
						btnUp.currentState = "selected";
						btnDown.currentState = "normal";
					}
					
					// perform the back end data call to cast the vote
					FrinkData.instance.api.vote(post.data.name, dir);
				} else {
					
					// if not logged in, show them the settings dialog
					_up = up;
					var s : SettingsModal = new SettingsModal();
					s.addEventListener(CloseEvent.CLOSE, showHandler);
					s.show(this.parent, "You'll need to log in to do that", this.makeVoteCallback);
					this.browser.visible = false;
				}
			}
			
			/**
			 * invoked from the settings screen when a login event occurs during an upvote
			 **/
			protected function makeVoteCallback() : void {
				this.makeVote(_up);
			}
			
			//--------------------------------------------------------------------------
			//
			//  Event Handlers
			//
			//--------------------------------------------------------------------------
			
			
			/**
			 * add the web browser thingy
			 **/
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				FrinkData.instance.addEventListener(FrinkEvent.MODAL_CLOSE, frink_modalClose);
				FrinkData.instance.addEventListener(FrinkEvent.MODAL_OPEN, frink_modalOpen);
				comments.addEventListener(Event.COMPLETE, commentList_completeHandler);
			}

			/**
			 * close this up and back to normal
			 **/
			protected function btnBack_clickHandler(event:MouseEvent):void
			{
				this.post = null;
				this.browser.visible = false;
				this.dispatchEvent(new Event(Event.CLOSE));
			}

			/**
			 * register up boat
			 **/
			protected function btnUp_clickHandler(event:MouseEvent):void
			{			
				this.makeVote(true);
			}
			
			/**
			 * register down boat
			 **/
			protected function btnDown_clickHandler(event:MouseEvent):void
			{
				this.makeVote(false);
			}
			
			/**
			 * switch to/from comments view
			 **/
			protected function btnComments_clickHandler(event:MouseEvent):void
			{
				if (!this.comments.visible) {
					this.currentState = "loading";
					this.comments.loadComments(this.post);
				} 
				this.browser.visible = this.comments.visible;
				this.comments.visible = !this.comments.visible;
				btnComments.currentState = (this.comments.visible ? "selected" : "normal");
			}
			
			/**
			 * open the post in a browser window
			 **/
			protected function btnWindow_clickHandler(event:MouseEvent):void
			{
				flash.net.navigateToURL(new URLRequest(post.data.url));
			}

			/**
			 * manage the visibility of this web control
			 **/
			protected function showHandler(event:FlexEvent):void
			{
				this.browser.visible = !_post.data.is_self;
			}
			
			/**
			 * manage the visibility of this web control
			 **/
			protected function hideHandler(event:FlexEvent):void
			{
				this.browser.visible = false;
			}
			
			/**
			 * re-show the stageWebView if neccesary
			 **/
			protected function frink_modalClose(event:FrinkEvent) : void {
				if (_post && _post.data)
					this.browser.visible = this.visible && !_post.data.is_self;
			}
			
			/**
			 * hide the stageWebView when any modal opens
			 **/
			protected function frink_modalOpen(event:FrinkEvent) : void {
				this.browser.visible = false;
			}
			
			/**
			 * hide the loading screen
			 **/
			protected function commentList_completeHandler(event:Event) : void {
				this.currentState = "normal";
			}

		]]>
	</fx:Script>	
	
	<s:states>
		<s:State name="normal" />
		<s:State name="loading" />
	</s:states>
	
	
	
	<s:Line left="0" top="0" bottom="0">
		<s:stroke>
			<s:SolidColorStroke color="0x515151" />
		</s:stroke>
	</s:Line>
	
	<s:VGroup width="100%" height="100%" gap="0">
		<!-- top bar -->
		<s:SkinnableContainer width="100%">
			<s:Rect left="0" right="0" top="0" bottom="1">
				<s:fill>
					<s:LinearGradient rotation="90">
						<s:GradientEntry color="0xf3f3f5" />
						<s:GradientEntry color="0xdedfe5" />
					</s:LinearGradient>
				</s:fill>
			</s:Rect>
			<s:Line left="0" right="0" bottom="0">
				<s:stroke>
					<s:SolidColorStroke color="0xa5a5a5" />
				</s:stroke>
			</s:Line>
			
			<s:HGroup width="100%" paddingBottom="10" paddingLeft="10" paddingRight="10" paddingTop="10" >
				
					<s:HGroup width="100%" paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5">
						
						<s:Group id="thumbnail" maskType="clip">
							<s:mask>
								<s:Group>
									<s:BorderContainer cornerRadius="5" width="{img.width}" height="{img.height}" />	
								</s:Group>
							</s:mask>
							<s:Image id="img" />
						</s:Group>
						
						
						<s:VGroup width="100%" fontSize="14">
							<s:Label text="{post.data.title}" width="100%" fontSize="18" />
							
							<s:HGroup width="100%" verticalAlign="bottom">
								<s:VGroup width="100%">
									<s:Label text="{post.data.domain}" width="100%"/>
									<s:HGroup fontSize="14">
										<s:Label text="{post.data.score}" color="0xFF0000"/>
										<s:Label text="{post.data.subreddit}"/>
										<s:Label text="by {post.data.author}"/>
									</s:HGroup>
									<s:Label text="{FrinkData.instance.getAgoLabel(post.data.created_utc)}"/>
								</s:VGroup>
								<s:HGroup>
									<s:Image source="@Embed(source='assets/icons/arrow-left.png')" id="btnBack" click="btnBack_clickHandler(event)" />
									
									<controls:SelectableButton 
										icon_normal="@Embed(source='assets/icons/arrow-up.png')"
										icon_selected="@Embed(source='assets/icons/arrow-up-selected.png')"
										id="btnUp" click="btnUp_clickHandler(event)" />
									
									<controls:SelectableButton 
										icon_normal="@Embed(source='assets/icons/arrow-down.png')"
										icon_selected="@Embed(source='assets/icons/arrow-down-selected.png')" 
										id="btnDown" click="btnDown_clickHandler(event)" />
									
									<controls:SelectableButton 
										icon_normal="@Embed(source='assets/icons/comments.png')"
										icon_selected="@Embed(source='assets/icons/comments-selected.png')"
										id="btnComments" click="btnComments_clickHandler(event)" visible="{post != null &amp;&amp; !post.data.is_self}" includeInLayout="{post != null &amp;&amp; !post.data.is_self}"/>
									
									<s:Image source="@Embed(source='assets/icons/action.png')" id="btnWindow" click="btnWindow_clickHandler(event)" />
								</s:HGroup>
							</s:HGroup>
							
							
						</s:VGroup>
						
						
						
					</s:HGroup>
				
				
				
				
			</s:HGroup> 
		</s:SkinnableContainer>
		
		
		<!-- post frame -->	
		<s:SkinnableContainer width="100%" height="100%">
			
			<controls:WebBrowser width="100%" height="100%" source="{post == null ? 'about:blank' : post.data.url}" id="browser" />	
			<panels:CommentList width="100%" height="100%" visible="false" id="comments" />
			<controls:LoadingBox includeIn="loading" />
		</s:SkinnableContainer>
		
	</s:VGroup>
	
</s:SkinnableContainer>
